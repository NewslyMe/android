import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

apply plugin: 'com.android.library'

boolean dash = false, hls = false, smoothstreaming = false

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 28)

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 20) // RN's minimum version
        targetSdkVersion safeExtGet("targetSdkVersion", 28)
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        // Make sure you have installed the react-native npm package before compiling
        url "../node_modules/react-native/android"
    }

    mavenCentral()
    google()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'
    implementation 'com.google.android.material:material:1.2.1'

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'com.google.code.gson:gson:2.8.8'

    def exoPlayerVersion = safeExtGet("exoPlayerVersion", '2.16.1')

    // ExoPlayer Core
    implementation "com.google.android.exoplayer:exoplayer-core:${exoPlayerVersion}"

    // ExoPlayer DASH
    if (dash) {
        implementation "com.google.android.exoplayer:exoplayer-dash:${exoPlayerVersion}"
    } else {
        compileOnly "com.google.android.exoplayer:exoplayer-dash:${exoPlayerVersion}"
    }

    // ExoPlayer HLS
    if (hls) {
        implementation "com.google.android.exoplayer:exoplayer-hls:${exoPlayerVersion}"
    } else {
        compileOnly "com.google.android.exoplayer:exoplayer-hls:${exoPlayerVersion}"
    }

    // ExoPlayer SmoothStreaming
    if (smoothstreaming) {
        implementation "com.google.android.exoplayer:exoplayer-smoothstreaming:${exoPlayerVersion}"
    } else {
        compileOnly "com.google.android.exoplayer:exoplayer-smoothstreaming:${exoPlayerVersion}"
    }

    implementation "com.google.android.exoplayer:exoplayer-ui:${exoPlayerVersion}"
    // Make sure we're using androidx
    implementation "androidx.core:core:1.3.0-rc01"
    implementation "androidx.media:media:1.1.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0-alpha01"
    implementation "com.github.bumptech.glide:glide:4.7.1"
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycle_version}"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
